plugins {
    // Default java settings
    id 'lightpermsx.java'

    // This module produces a shaded .jar plugin file
    id 'lightpermsx.shade-all'
}

repositories {
    // for spigot-api
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // for bungeecord-chat, a transitive dependency of spigot-api in old versions
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    // for VaultAPI
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation project(':core')
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

// Expand ${version} in plugin.yml
processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand(['version': project.version])
    }
}

// Shaded .jar should be called "lightpermsx-bukkit.jar" instead of just "bukkit.jar"
shadowJar {
    archiveBaseName = 'lightpermsx-bukkit'
}

// Copy the shaded .jar to the test server
tasks.register("exportToBukkitTestServer", Copy) {
    dependsOn build
    from shadowJar.archiveFile
    into 'C:\\mctest\\plugins'
}